#!/usr/bin/perl
# submerge; see README.md for usage instructions

use strict;
use warnings;

use lib 'lib';

use Mojolicious::Lite;
use Submerge::Feed;
use Submerge::RSS;
use Submerge::YouTubeFeed;
use YAML;

# config is in production.yaml or development.yaml
my $config_file = app->mode . ".yaml";
die "Can't read from submerge config file ($config_file), start by copying the default development.yaml\n"
    unless -r $config_file;
my $config = YAML::LoadFile($config_file);

die "base_url is missing from submerge config file ($config_file)"
    unless defined $config->{base_url};

Submerge::YouTubeFeed->max_feed_cache($config->{max_feed_cache})
    if defined $config->{max_feed_cache};

hook before_render => sub {
    my ($c) = @_;
    $c->stash(config => $config);
};

helper feed => sub {
    my ($c) = @_;

    my $token = scalar $c->param('token');
    die "no token" if !$token;

    return Submerge::Feed->new($token);
};

# URLs like /feeds/videos.xml?channel_id=UCzr30osBdTmuFUS8IfXtXmg
# We're just copying the YouTube URL format
get '/feeds/videos.xml' => sub {
    my ($c) = @_;

    my @channels = split /,/, scalar $c->param('channel_id');

    my $submerge_feed = Submerge::RSS->new();

    my @feeds;
    my $nfeeds = 0;
    my $add_feed = sub {
        my ($feed) = @_;

        $submerge_feed->add_feed($feed);

        $nfeeds++;
        if ($nfeeds == @channels) {
            my $this_url = $c->req->url;
            my $send_feed = $submerge_feed->feed;
            $send_feed->self_link("$config->{base_url}$this_url");

            $c->res->headers->content_type("application/rss+xml");
            return $c->render(data => $send_feed->as_xml);
        }
    };

    for my $channel_id (@channels) {
        Submerge::YouTubeFeed->get_feed($channel_id, sub {
            my ($feed) = @_;
            $add_feed->($feed);
        });
    }
};

get '/rss/:token' => sub {
    my ($c) = @_;
    # TODO: temporary redirect to /feeds/videos.xml
    # TODO: just render $c->feed->rss->as_xml
};

get '/' => sub {
    my ($c) = @_;

    if ($config->{public}) {
        return $c->render('public_index');
    } else {
        return $c->render('private_index');
    }
};

get '/submerge' => sub { shift->redirect_to('/'); };

sub add_channel {
    my ($c, $channel_id, $cb) = @_;

    $c->session(channels => []) if !$c->session('channels');

    # do nothing if already subscribed to this channel
    return $cb->() if grep { $_->{channel_id} eq $channel_id } @{ $c->session('channels') };

    Submerge::YouTubeFeed->get_feed($channel_id, sub {
        my ($feed) = @_;

        if ($feed) {
            push @{ $c->session('channels') }, {
                name => $feed->title,
                channel_id => $channel_id,
            };
        } else {
            # or else what?
        }
        $cb->();
    });
}

post '/subscribe/:token' => sub {
    my ($c) = @_;

    Submerge::YouTubeFeed->get_channel_id(scalar $c->param('url'), sub {
        my ($channel_ids, $error) = @_;

        if ($error) {
            return $c->render(json => {error => $error});
        } else {
            # TODO: check that the generated rss feeds look sound (even if there are no videos) before giving it to the user
            $c->feed->add_channel($_) for @$channel_ids;
            return $c->redirect_to('/subs/' . (scalar $c->param('token')));
        }
    });
};

post '/clear-subs/:token' => sub {
    my ($c) = @_;
    $c->feed->clear_channels;
    return $c->redirect_to('/subs/' . (scalar $c->param('token')));
};

post '/unsubscribe/:token' => sub {
    my ($c) = @_;
    $c->feed->remove_channel(scalar $c->param('channel_id'));
    return $c->redirect_to('/subs/' . (scalar $c->param('token')));
};

get '/bulk-import/:token' => sub {
    my ($c) = @_;
    return $c->render('bulk-import');
};

post '/bulk-import/:token' => sub {
    my ($c) = @_;

    my $opml = scalar $c->param('opml');
    die "no file" if !$opml;
    my $data = $opml->slurp;
    die "no bytes" if !$data;
    die "not opml" if $data !~ /^<opml/;

    my @channel_ids;

    while ($data =~ /\/feeds\/videos.xml\?channel_id=([^"]*)/g) {
        $c->feed->add_channel($1);
    }

    return $c->redirect_to('/');
};

get '/subs/:token' => sub {
    my ($c) = @_;

    $c->feed->channels(sub {
        return $c->render(json => [@_]);
    });
};

app->config($config->{mojo_config});
app->start;
